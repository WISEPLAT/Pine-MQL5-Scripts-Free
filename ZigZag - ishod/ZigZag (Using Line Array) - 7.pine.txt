// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© HeWhoMustNotBeNamed

//@version=4
study("ZigZag (Using Line Array)", shorttitle="ZigZag", overlay=true, max_lines_count=500, max_labels_count=500)
source=input(close)
Length = input(10, minval=0, step=5)
DeviationThreshold = 0
showPivots = input(false)
showRatios = input(true)
bullishColor = input(color.green)
bullTrapColor = input(color.orange)
bearishColor = input(color.red)
bearTrapColor = input(color.lime)

var zigzaglines = array.new_line(0)
var zigzaglabels = array.new_label(0)
var zigzagdir = array.new_int(0)
var zigzagratios = array.new_float(0)

int max_array_size = 20

pivots(length, highSource, lowSource)=>
    float ph = highestbars(highSource, length) == 0 ? highSource : na
    float pl = lowestbars(lowSource, length) == 0 ? lowSource : na
    dir = 0
    dir := iff(ph and na(pl), 1, iff(pl and na(ph), -1, dir[1]))
    [dir, ph, pl]

get_edir(dir, y2)=>
    eDir = dir
    if(array.size(zigzaglines) > 0)
        lastLine = array.get(zigzaglines, 0)
        lastPivot = line.get_y1(lastLine)
        eDir := (dir*y2 > dir*lastPivot? 2 : 1) * dir
    lineColor = eDir == 2? bullishColor : eDir == 1? bullTrapColor : eDir == -1? bearTrapColor : bearishColor
    [eDir, lineColor]

add_to_zigzaglines(x1, y1 , x2, y2, dir)=>
    [eDir, lineColor] = get_edir(dir, y2)

    zline = line.new(x1=x1, y1=y1, 
         x2 = x2, y2=y2,
         color=lineColor, width=2, style=line.style_solid)
    array.unshift(zigzaglines, zline)

add_to_zigzaglabels(x1, x2, y1, y2, dir)=>
    [eDir, lineColor] = get_edir(dir, y2)
    pivotLabel = eDir == 2? "HH" : eDir == 1? "LH" : eDir == -1? "HL" : "LL"
    lastLineLen = 0.0
    currentLineLen = abs(y2-y1)
    if(array.size(zigzaglines) > 0)
        lastLine = array.get(zigzaglines, 0)
        lastLineLen := abs(line.get_y2(lastLine) - line.get_y1(lastLine))

    ratio = round(lastLineLen != 0 ? currentLineLen/lastLineLen : 0, 3)
    labelText = (showPivots ? pivotLabel : "") +
                         (showPivots and showRatios ? " - " : "") +
                         (showRatios ? tostring(ratio) : "")
    yloc = source == close ? (dir > 0? yloc.abovebar : yloc.belowbar) : yloc.price
    labelStyle = dir > 0? label.style_label_down : label.style_label_up
    labelSize = showRatios and showPivots? size.normal : size.small
    zlabel = label.new(x=x2, y=y2, text=labelText, xloc = xloc.bar_index, yloc = yloc, 
                     color=lineColor, size=labelSize, style=labelStyle)
    if(not showRatios and not showPivots)
        label.delete(zlabel)
    array.unshift(zigzaglabels,zlabel)
    array.unshift(zigzagdir, eDir)
    array.unshift(zigzagratios, ratio)

add_to_zigzag(dir, dirchanged, ph, pl, index, highSource, lowSource)=>
    value = (dir == 1? ph : pl)

    y1= dir == 1? lowest(lowSource, Length) : highest(highSource, Length)
    x1 = bar_index + (dir == 1? lowestbars(lowSource, Length) : highestbars(highSource,Length))
    x2 = index
    y2 = value
    skip = false
    if(array.size(zigzaglines) > 0)
        lastLine = array.get(zigzaglines, 0)
        lastValue = line.get_y2(lastLine)
        lastIndex = line.get_x2(lastLine)
        
        if(not dirchanged)
            if(dir == 1 and value < lastValue) or (dir == -1 and value > lastValue)
                skip := true
            else
                line.delete(array.shift(zigzaglines))
                label.delete(array.shift(zigzaglabels))
                array.shift(zigzagdir)
                array.shift(zigzagratios)
        if(array.size(zigzaglines) > 0)
            lastLine = array.get(zigzaglines, 0)
            lastValue = line.get_y2(lastLine)
            lastIndex = line.get_x2(lastLine)
            x1 := lastIndex
            y1 := lastValue

    outsideDeviationThreshold = ((abs(y1-y2)*100/y1) > DeviationThreshold)
    if(outsideDeviationThreshold and not skip)
        add_to_zigzaglabels(x1, x2, y1, y2, dir)
        add_to_zigzaglines(x1, y1 , x2, y2, dir)
    if array.size(zigzaglines) > max_array_size
        array.pop(zigzaglines)
        array.pop(zigzaglabels)
        array.pop(zigzagdir)
        array.pop(zigzagratios)

zigzag(length, DeviationThreshold, highSource, lowSource)=>
    [dir, ph, pl] = pivots(length, highSource, lowSource)
    dirchanged = change(dir)
    if(ph or pl)
        add_to_zigzag(dir, dirchanged, ph, pl, bar_index, highSource, lowSource)

highSource = source == close? high : source
lowSource = source == close? low : source

zigzag(Length, DeviationThreshold, highSource, lowSource)
